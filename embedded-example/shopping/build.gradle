plugins {
    id 'application'
    id "com.google.cloud.tools.jib" version "$jibVersion"
    id "org.jetbrains.kotlin.plugin.serialization" version "$kotlinVersion"
}

apply plugin: 'idea'
apply plugin: 'kotlin'
apply plugin: "kotlin-kapt"

archivesBaseName = 'statefun-shopping'
mainClassName = 'shopping.Harness'
//mainClassName = 'shopping.StateBootstrap'
sourceCompatibility = 1.11
targetCompatibility = 1.11

configurations {
    provided
    distribution

    localCompileClasspath.extendsFrom compileClasspath
    localRuntimeClasspath.extendsFrom runtimeClasspath

    implementation.exclude group: 'org.apache.commons'
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-core:1.0.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.0.0'

    compile project(':protocols')
    implementation 'de.vandermeer:asciitable:0.3.2'

    provided "com.google.protobuf:protobuf-java:$protobufVersion"
    provided "com.google.protobuf:protobuf-java-util:$protobufVersion"
    provided "org.apache.flink:statefun-sdk:$statefunVersion"
    provided "org.apache.flink:statefun-flink-harness:$statefunVersion"
    compile "org.apache.flink:statefun-flink-state-processor:$statefunVersion"
    compile "org.apache.flink:flink-state-processor-api_2.12:$flinkVersion"
    compile "org.apache.flink:flink-statebackend-rocksdb_2.12:$flinkVersion"

    provided "org.apache.logging.log4j:log4j-slf4j-impl:2.13.3"
    provided "org.slf4j:slf4j-api:1.7.28"

    compile 'com.google.auto.service:auto-service-annotations:1.0-rc6'
    kapt 'com.google.auto.service:auto-service:1.0-rc6'

    distribution "org.apache.flink:statefun-flink-distribution:$statefunVersion"
}

sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided

    local {
        compileClasspath += configurations.provided
        runtimeClasspath += main.output
        runtimeClasspath += configurations.provided
    }
}

run {
    applicationDefaultJvmArgs = ["-Dlog4j2.configurationFile=log4j2.properties"]
    classpath = sourceSets.local.runtimeClasspath
}

jib {
    from.image = "flink:1.11.3-java11"
    to.image = "statefun-shopping"

    containerizingMode = 'packaged'
    container {
        creationTime = 'USE_CURRENT_TIMESTAMP'
        entrypoint = 'INHERIT'
        appRoot = '/opt/flink/usrlib'
    }

    extraDirectories {
        paths {
            path {
                from = file("${buildDir}/statefun-distribution")
                into = '/opt/flink/usrlib/libs/'
            }
        }
    }
}

task copyDistribution(type: Copy) {
    from configurations.distribution
    into "${buildDir}/statefun-distribution"
    include("statefun-flink-distribution*")
}

tasks.jibDockerBuild.dependsOn copyDistribution
